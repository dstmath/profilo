load("//buck_imports:profilo_path.bzl", "profilo_path")
load("//build_defs:fb_xplat_cxx_library.bzl", "fb_xplat_cxx_library")

def tracer_library(version):
    version_ = version.replace(".", "_")
    version_num = version.replace(".", "")

    exported_headers = [
        "ArtCompatibility.h",
        "ArtTracer.h",
    ]
    fb_xplat_cxx_library(
        name = "tracer-{version}".format(version=version),
        srcs = [
            "ArtCompatibility.cpp",
            "ArtTracer.cpp",
        ],
        compiler_flags = [
            "-fvisibility=hidden",
            "-fexceptions",
            "-std=gnu++14",
            "-DLOG_TAG=\"Profilo/ArtCompat\"",
            "-UMUSEUM_VERSION", "-DMUSEUM_VERSION=v{}_readonly".format(version_),
            #'-DFBLOG_NDEBUG=0', # extra logging
        ],
        exported_headers = {
            header.replace(".h", "_" + version_num + ".h"): header for header in exported_headers
        },
        exported_preprocessor_flags = [
            "-DMUSEUM_VERSION_{}".format(version_),
        ],
        force_static = True,
        header_namespace = "profiler",
        headers = glob(
            ["*.h"],
            excludes = exported_headers,
        ),
        reexport_all_header_dependencies = False,
        soname = "libprofiloprofiler{version_num}.$(ext)".format(version_num=version_num),
        visibility = [
            profilo_path("..."),
        ],
        deps = [
            profilo_path("deps/fb:fb"),
            profilo_path("deps/fbjni:fbjni"),
            profilo_path("deps/forkjail:forkjail"),
            profilo_path("cpp/museum-readonly/{version}/art/runtime:runtime".format(version=version)),
            profilo_path("cpp/logger:logger"),
        ],
    )

tracer_library(version = "5.1.1")

tracer_library(version = "6.0.1")

tracer_library(version = "7.0.0")

PROFILER_EXPORTED_HEADERS = [
    "DalvikTracer.h",
    "SamplingProfiler.h",
]

fb_xplat_cxx_library(
    name = "constants",
    header_namespace = "profiler",
    exported_headers = [
        "Constants.h",
    ],
    visibility = [
        profilo_path("cpp/writer/..."),
    ],
)

fb_xplat_cxx_library(
    name = "base_tracer",
    header_namespace = "profiler",
    exported_headers = [
        "BaseTracer.h",
    ],
    visibility = [
        profilo_path("..."),
    ],
)

fb_xplat_cxx_library(
    name = "profiler",
    srcs = [
        "DalvikTracer.cpp",
        "SamplingProfiler.cpp",
        "jni.cpp",
    ],
    headers = glob(
        ["*.h"],
        excludes = PROFILER_EXPORTED_HEADERS,
    ),
    header_namespace = "profiler",
    exported_headers = PROFILER_EXPORTED_HEADERS,
    allow_jni_merging = True,
    compiler_flags = [
        "-fvisibility=hidden",
        "-fexceptions",
        "-DLOG_TAG=\"Profilo/Profiler\"",
        "-Wno-format-security",
        "-Wno-unknown-attributes",
        #'-DFBLOG_NDEBUG=0', # verbose logging
        "-std=gnu++14",
    ],
    soname = "libprofilo_stacktrace.$(ext)",
    supported_platforms_regex = "^android-(armv7|x86)$",
    visibility = [
        profilo_path("java/main/com/facebook/profilo/provider/stacktrace:"),
    ],
    deps = [
        ":base_tracer",
        ":constants",
        ":tracer-5.1.1",
        ":tracer-6.0.1",
        ":tracer-7.0.0",
        profilo_path("cpp:constants"),
        profilo_path("cpp:profilo"),
        profilo_path("cpp:providers"),
        profilo_path("cpp/logger:logger"),
        profilo_path("cpp/util:util"),
        profilo_path("deps/dalvik:dalvik-subset"),
        profilo_path("deps/fb:fb"),
        profilo_path("deps/fbjni:fbjni"),
        profilo_path("deps/museum:museum"),
        profilo_path("deps/sigmux:sigmux"),
    ],
)
